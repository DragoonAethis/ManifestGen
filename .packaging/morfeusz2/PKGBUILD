# Maintainer: Dragoon Aethis <dragoon@dragonic.eu>
# Maintainer: Jan Rydzewski <flegmer@gmail.com>

pkgname=(morfeusz2 python-morfeuszbuilder python-morfeusz2)
pkgver=20210131
pkgrel=1
pkgdesc="Morfeusz morphological analyzer"
arch=('x86_64')
url='http://morfeusz.sgjp.pl/'
license=('BSD')

makedepends=('python' 'python-setuptools' 'swig')

source=("LICENSE"
        "http://sgjp.pl/morfeusz/download/$pkgver/morfeusz-src-$pkgver.tar.gz")
sha256sums=("16c7ca379ef9fb6368c20d0ee71a9c83dd9c55e3b9fc34aade443c33d731d829"
            "18fe78e12bc58c8f83b80c4b7b891ef742fc17667dc3ee2fb728c8e83554c93f")

build() {
	cd "$srcdir/Morfeusz"
	cmake \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-D SKIP_DICTIONARY_BUILDING=1 \
		-D DEFAULT_DICT_NAME=sgjp \
		-D EMBEDDED_DEFAULT_DICT=0 \
		-D PY=$(python3 --version | cut -f 2 -d ' ' | cut -f 1,2 -d '.') \
		.
	make libmorfeusz gui-jar morfeusz_analyzer morfeusz_generator morfeusz_analyzer_old
	make CXX_FLAGS=-std=c++11 test_runner
}

package_morfeusz2() {
	depends=('gcc-libs')

	cd "$srcdir/Morfeusz"
	make DESTDIR="$pkgdir/" install
	install -D -t "$pkgdir/usr/share/licenses/$pkgname" "$srcdir/LICENSE"
}

package_python-morfeuszbuilder() {
	pkgdesc="Dictionary builder for the Morfeusz morphological analyzer"
	depends=('python' 'python-pyparsing')
	arch=('any')

	cd "$srcdir/Morfeusz/fsabuilder"
	python setup.py install --root="$pkgdir/" --optimize=1
	install -D -t "$pkgdir/usr/share/licenses/$pkgname" "$srcdir/LICENSE"
}

package_python-morfeusz2() {
	pkgdesc="Python wrapper for the Morfeusz morphological analyzer"
	depends=('morfeusz2' 'python')

	cd "$srcdir/Morfeusz/morfeusz/wrappers/python3"
	python setup.py install --root="$pkgdir/" --optimize=1
	install -D -t "$pkgdir/usr/share/licenses/$pkgname" "$srcdir/LICENSE"
}
